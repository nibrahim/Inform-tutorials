!% -SD

!%==================================================
Constant Story "Heidi";
Constant Headline
    "^ A simple Inform example
    ^by Roger Firth and Sonia Kesserich.^";
Constant MAX_CARRIED 1;
Include "Parser";
Include "Verblib";

!%==================================================
! The game objects



Object before_cottage "In front of a cottage"
 with 	description
	    "You stand outside a cottage. The forest stretches east.",
	e_to forest,
 has 	light;

Object forest "Deep in the forest"
 with   description
	    "Through the dense foliage, you glimpse a building to the west.
	    A track heads to the northeast.",
	w_to before_cottage,
	ne_to clearing,
 has 	light;

Object bird "baby bird" forest
 with 	description "Too young to fly, the nestling tweets helplessly.",
	name 'baby' 'bird' 'nestling',
 has	;

Object clearing "A forest clearing"
 with 	description
	    "A tall sycamore tree stands in the middle of this clearing. The path
	    winds southwest through the trees.",
	sw_to forest,
	u_to top_of_tree,
 has 	light;

Object tree "tall sycamore tree" clearing
 with 	description
	    "Standing proudly in the middle of the clearing, the 
	    tree looks easy to climb",
	name 'tall' 'sycamore' 'tree' 'stout' 'proud',
 has 	scenery;


Object 	nest "bird's nest" clearing
 with 	description "The nest is carefully woven of twigs and moss.",
	name 'bird^s' 'nest' 'twigs' 'moss',
 has 	container open;

Object top_of_tree "At the top of the tree"
 with 	description "You cling precariously to the trunk.",
	d_to clearing,
 has 	light;

Object	branch "wide firm bough" top_of_tree
 with 	description "It's flat enough to support a small object",
	name 'wide' 'firm' 'flat' 'bough' 'branch',
	each_turn [;
	    if (nest in branch)
		deadflag = 2;
	],
 has 	static supporter;





!%==================================================
! Entry point routines

[ Initialise; location = before_cottage; ];

!%==================================================
! Standard and extended grammar

Include "Grammar";

!%==================================================



	    

